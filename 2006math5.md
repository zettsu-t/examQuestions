# 麻布中学校 2006年 入試問題 算数 問5

問題は以下の通りです。

http://www.inter-edu.com/nyushi/2006/junior/azabu/index.php

## ソースコードを実行して解を表示する

Cygwinターミナルから、makeを引数なしで実行すると、ビルド、実行、実行結果の検査を行います。

```bash
make
```

## 総当たりで解く

Haskellで書いたコードは以下の通りです。GHC 8.0.1 で実行できます。本ページの全ソースコードは[2006math5.hs](2006math5.hs)です。

```haskell
-- 解を読めるように整形する
toString (o:a:b:c:d:e:f:_) = (row [a,b,c]) ++ (center o) ++ (row [d,e,f]) ++ "-----\n"
  where row (i:j:k:_) = (show i) ++ " " ++ (show j) ++ " " ++ (show k) ++ "\n"
        center i = "  " ++ show i ++ "  \n"

-- マスに入れた数字が、解であるかどうか判定する
isSolution (o:a:b:c:d:e:f:_) = foldr compare True [bottom, column, slash, backslash]
  where compare i accum = (i == top) && accum
        top       = a + b + c
        bottom    = d + e + f
        column    = b + o + e
        slash     = d + o + c
        backslash = a + o + f

-- 四隅のマスのうち、左上に最も小さい数字がくるように並び替える
sortSolution = flipHorizontal.flipVertical.rotate
  where flipHorizontal arg@(o:a:b:c:d:e:f:_) | a > c = [o,c,b,a,f,e,d]
                                             | otherwise = arg
        flipVertical arg@(o:a:b:c:d:e:f:_) | a > d = [o,d,e,f,a,b,c]
                                           | otherwise = arg
        rotate arg@(o:a:b:c:d:e:f:_) | a > f = [o,f,e,d,c,b,a]
                                     | otherwise = arg

-- 反転回転を考慮して一意な解の集まりを文字列にする
formatSolutions xs = concatMap toString $ nub.sort $ map sortSolution xs

-- 1..9の順列をマスに当てはめて解を選ぶ
solutions1 = formatSolutions $ filter isSolution allNumberSet
  where allNumberSet = map (take 7) $ permutations [1..9]
```

GHCで実行するとこのように出力されます。縦書きの出力を横に直したものが以下の表です。中央が6の解は、中央が4の解のそれぞれの数字に2を足したものです。

|解1|解2|解3|解4|解5|解6|
|:-----:|:-----:|:-----:|:-----:|:-----:|:-----:|
|1 5 6|1 6 5|2 7 3|3 7 8|3 8 7|4 9 5|
|  4  |  4  |  4  |  6  |  6  |  6  |
|2 3 7|3 2 7|5 1 6|4 5 9|5 4 9|7 3 8|

|解7|解8|解9|解10|解11|解12|
|:-----:|:-----:|:-----:|:-----:|:-----:|:-----:|
|1 6 8|1 8 6|2 6 7|2 7 6|2 9 4|3 8 4|
|  5  |  5  |  5  |  5  |  5  |  5  |
|2 4 9|4 2 9|3 4 8|4 3 8|6 1 8|6 2 7|

## 総当たりの範囲を狭める

直線上に並んでいる3個の数字の和は、縦横斜めいずれも等しい、ということを条件にして、探す解の範囲を狭めてみます。

```haskell
sizeOfCells = 7
digits = [1..9]

-- 和がsとなる、異なる一桁の数字二個の組を選ぶ。先頭の数字の方が小さい。
pairSet s = filter f $ map pair [1..((s - 1) `div` 2)]
  where pair i = [i, s - i]
        f (l:r:_) = l < (last digits) && r > (head digits) && r <= (last digits)

-- ある数字の組と、反転した組を返す
mirrorPair (l:r:_) = [[l,r], [r,l]]

-- 中心にoを置き、縦横斜めの3個の数字を足すとcellsSumになり、
-- afdcにfilledを置き、pairとその前後を入れ替えたものをbeに置くすべての解
setColumn o cellsSum _ filled pair = map f (mirrorPair pair)
  where f nextPair = check (filled ++ nextPair)
        check (a:f:d:c:b:e:_) |
          (a+b+c) == cellsSum && (d+e+f) == cellsSum = Just [o,a,b,c,d,e,f]
        check _ = Nothing

-- pairとその前後を入れ替えたものを、nextFuncで置くすべての解
apply nextFunc o cellsSum pairs filled pair = concatMap f newPairs
  where newPairs = delete pair pairs
        f nextPair = concatMap (g nextPair) (mirrorPair pair)
        g nextPair p = nextFunc o cellsSum newPairs (filled ++ p) nextPair

-- 中心にoを置き、縦横斜めの3個の数字を足すとcellsSumになり、
-- afにfilledを置き、pairとその前後を入れ替えたものをdcに置くすべての解
setSlash o cellsSum pairs filled pair = apply setColumn o cellsSum pairs filled pair

-- 中心にoを置き、縦横斜めの3個の数字を足すとcellsSumになり、
-- pairとその前後を入れ替えたものをafに置くすべての解
setBackslash o cellsSum pairs pair = apply setSlash o cellsSum pairs [] pair

-- 中心にoを置き、縦横斜めの3個の数字を足すとcellsSumになるような、すべての解
withSum o cellsSum = concatMap f pairs
  where f p = setBackslash o cellsSum pairs p
        pairs = pairSet (cellsSum - o)

-- 中心にoを置いたときのすべての解
atCenter o = concatMap (withSum o) [sumMin..sumMax]
  where rest = delete o digits
        sizeOfUsedDigits = sizeOfCells - 1
        sizeOfUnusedDigits = (length rest) - sizeOfUsedDigits
        sumMin = o + (head rest) + (last $ take sizeOfUsedDigits rest)
        sumMax = o + (head (drop sizeOfUnusedDigits rest)) + (last rest)

solutions2 = formatSolutions $ concatMap catMaybes $ map atCenter digits
```

実行時間を比較するために、GHCでexeファイルを作成してから実行します。引数は必須で、引数に1または2または3を指定することで、solutions1, solutions2, solutions3 (後述)のどれで解くか指定します。実行時間を比較すると、確かに二番目の方が速いです。

```text
time ./2006math5 1 > /dev/null
real    0m0.082s
user    0m0.000s
sys     0m0.000s

time ./2006math5 2 > /dev/null
real    0m0.031s
user    0m0.000s
sys     0m0.000s
```

## もっと総当たりの範囲を狭める

中心を取り囲む6個のマスについて、縦斜め3個ずつの和が等しいです。中心は共通なので、点対称の位置にある2マスの和が、縦と斜めについて等しいと言えます。ある和が与えられたとき、和が等しくなるような2個の数を、少なくとも3組作れるでしょうか。

1..9から中心を除いた残り8個の数字について、最大最小の数から挟みうちにして対にして除くと、3組取れそうです。例えば5が中心なら、{1-9, 2-8, 3-7, 4-6}が取れます。これは4組取れましたが3組あればよいので、先頭を最大最小からずらして、{1-8, 2-7, 3-6}, {2-9, 3-8, 4-7}でもよいです。1-7、3-9から始めると3組取れません。

1..9を中心に置いたとき、どのような和について、和が等しくなるような2個の数を少なくとも3組作れるか、確かめてみましょう。

```haskell
-- 和がsとなる、異なる一桁の数字二個の組を選ぶ。先頭の数字の方が小さい。
-- ただし組の要素にoを含まない
pairSetExcept s o = filter f $ map pair [1..((s - 1) `div` 2)]
  where pair i = [i, s - i]
        f (l:r:_) = l /= o && r /= o && l < r && r <= 9

-- 中心に1..9をそれぞれ置いたときに、
-- 和がsとなる数字二個の組を3組以上作れるようなsの集合
-- ただし各組の要素にoを含まない
centerAndSums = map f digits
  where f o = (o, [x | x <- range, hasThreePairs x o])
        range = [(head digits)..((last digits)*2)]
        hasThreePairs s o = (length (pairSetExcept s o)) >= 3
```

```text
[(1,[9,10,11,12,13]),(2,[9,10,11,12,13]),(3,[9,10,11,13]),
 (4,[8,9,10,11]),(5,[9,10,11]),(6,[9,10,11,12]),
 (7,[7,9,10,11]),(8,[7,8,9,10,11]),(9,[7,8,9,10,11])]
```

ところですべてのマスの数字を、複数回足してもよいとして、足すといくつになるでしょうか。以下のように定義すると、これらは同じ値になります。

1. 縦1本、横2本、斜め2本の和を足す。これらは皆等しいので、これらの合計は5の倍数になる。
1. 中心の数字を3回、それ以外のマスの数字を2回足す。
1. 上記を言い換えて、中心からみて点対象の位置にある2マスの和(先ほど求めた対)を6回、中心の数字を3回足す。
1. 上記をさらに言い換えて、点対称な対2マスの和を2倍して中心の数字を足してから、3倍する。

上記の定義1と定義4より、5と3は互いに素なので、以下の条件が付くように対を絞ってみましょう。
* 直線上の3マスの和は、3の倍数に限る
* 点対称な対2マスの和 * 2 + 中心のマスは、5の倍数に限る

```haskell
centerAndSum35 = filter g $ map f centerAndSums
  where f (o,xs) = (o, [s | s <- xs, match s o])
        g (o,xs) = not $ null xs
        match s o = ((o + s) `rem` 3 == 0) && ((o + s * 2) `rem` 5 == 0)
```

```text
[(4,[8]),(5,[10]),(6,[12])]
```

だいぶ絞れてきました。ここから探してみましょう。四隅のマスのうち最も小さい数字は左上にくる、と仮定しても一般性を失わないのでそうします。

|中心|対1|対2|対3|対4|
|:----:|:---:|:---:|:---:|:---:|
|4|1,7|2,6|3,5|-|
|6|3,9|4,8|5,7|-|
|5|1,9|2,8|3,7|4,6|

### 中心が4,6

```text
1 c d
  4
a b 7
```

とすると、a+b=5 ですので、(a,b) = (2,3) または (3,2)です。これから(c,d) = (5,6), (6,5)を導けます。

```text
2 c d
  4
a b 6
```

とすると、a+b=6 ですので、(a,b) = (1,5) または (5,1)ですが、a=1は既に求めた(左上が1の場合に含む)ので、(a,b,c,d) = (5,1,7,3) を導けます。

```text
3 c d
  4
a b 5
```

とすると、a+b=7 ですので、(a,b) = (1,6) または (6,1) ですが、a=1は既に求めましたし、a=6ならd=2ですがこれも求めましたので改めて数えません。よって以上が解のすべてです。

中心が6のときは、中心が4のときの解に対して、各マスの数字に2を足したものです。

### 中心が5

一気に求めましょう。

```text
1 c d | 1 c d | 2 c d | 2 c d | 2 c d | 3 c d |
  5   |   5   |   5   |   5   |   5   |   5   |
a b 9 | a b 9 | a b 8 | a b 8 | a b 8 | a b 7 |
```

|マスの組|対1|対2|対3|対4|対5|対6|
|:-----:|:---:|:---:|:---:|:---:|:---:|:---:|
|a,b|2,4|4,2|3,4|4,3|6,1|6,2|
|c,d|6,8|8,6|6,7|7,6|9,4|8,4|

左上が4のときは、aとdを同時に4より大きくできないので改めて数えません。よって以上が解のすべてです。

最終的にこのようなコードになります。二番目の解法と同じ出力をしますが、実は二番目より実行速度が速くなっていないです。

```haskell
atCenter3 o = concatMap f [sumMin..sumMax]
  where rest = delete o digits
        sizeOfUsedDigits = sizeOfCells - 1
        sizeOfUnusedDigits = (length rest) - sizeOfUsedDigits
        sumMin = o + (head rest) + (last $ take sizeOfUsedDigits rest)
        sumMax = o + (head (drop sizeOfUnusedDigits rest)) + (last rest)
        f s | (s `rem` 3 == 0) && ((s + s - o) `rem` 5 == 0) = withSum o s
            | otherwise = [Nothing]

solutions3 = formatSolutions $ concatMap catMaybes $ map atCenter3 digits
```

## 中心に4,5,6しか置けない理由

中心に4,5,6を置けるが、1,2,3,7,8,9を置けない理由について考察しましょう。

マスを左上、右下、左下、右上、左上とたどって(そごうのマークですね)、直線上のマスを埋めていきます。a,b,cの配置はこれまでの説明とは違うので注意してください。

```text
a _ _ | a _ _ |   a   _ _ |   a   _  b+c-o |   a   2*o-c b+c-o |
  o   |   o   |       o   |       o        |         o         |
_ _ b | _ c b | a+o-c c b | a+o-c c    b   | a+o-c   c     b   |
```

縦の3マスの数字を足すと、3 * o になります。直線上の3マスの和は3の倍数に限る、というのは問題の解ではいつでも成り立つことが分かります。しかし3 * oが大きすぎたり小さすぎたりすると、他の数字の置き場所がなくなりそうです。

o = 1または2、つまり3 * o = 3または6だと、7,8,9を置いただけでその直線上の数字の和は6を超えてしまいます。そして置かない数字は2個しか選べないので、最も小さい7をマスに入れたとしても、6を超えることは避けようがないです。よって中心に1と2は置けません。中心に3をおいても 3 + 7 > 3 * 3 なので、やはり中心に3は置けません。

o = 7とすると、逆に直線上の数字の和が21に満たない、ということが起きます。なぜなら1と2を捨てて3をマスに入れたとしても、直線上の数字の和を21にするためには11が必要になりますが、置ける数字は一桁でしたね。o = 8,9でも同様ですので、中心に7,8,9は置けません。

実は解に対して、すべてのマスを10から引いた値(1なら9、9なら1)に置き換えても解になります。なぜなら各直線上の数字の和sが、30-sに変わるだけだからです。同じ理由で、解でないものに対して、すべてのマスを10から引いた値に置き換えたものは解になりません。確かめてみましょう。

```haskell
swapSolutions = (original == swapped, [original, swapped])
  where original = unique $ filter isSolution allNumberSet
        swapped  = unique $ map (map (\x -> 10 - x)) original
        allNumberSet = map (take 7) $ permutations [1..9]
        unique xs = nub.sort $ map sortSolution xs
```

```text
(True,[[[4,1,5,6,2,3,7], ... ,[6,4,9,5,7,3,8]],
       [[4,1,5,6,2,3,7], ... ,[6,4,9,5,7,3,8]]])
```

合っているようです。つまり1,2,3を中心に置けないことと、9,8,7を中心に置けないことは対称性があります。そして4を中心に置けるなら6も置けるということです。

最後に、中心 * 3 以外の数は直線上の数の和にはならないので、中心が4,5,6の場合でも、点対称のマスの和はそれぞれ8,10,12に限ることが分かります。これなら、プログラミングなしでこの問題を解けそうですね。
